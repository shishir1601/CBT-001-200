PROC 1 CMPROPT
  ISPEXEC VGET (SLSS DEBUG CHGFLG)
  IF &DEBUG = DEBUG THEN DO
      CONTROL MSG LIST CONLIST SYMLIST
  END DO
  ELSE DO
      CONTROL NOMSG
  END DO
BEGIN: +
  IF &CMPROPT = &STR(STOP) THEN GOTO STOP
  IF &CHGFLG = &STR(Y) THEN DO
    SET SLSSERR1 = UPDATE(S) HAVE BEEN MADE TO THE SLSS TABLE FILE.
    SET SLSSERR2 = THIS UTILITY REQUIRES THAT THE SLSS TABLE FILE IS NOT
    SET SLSSERR3 = UPDATED DURING THE CURRENT SESSION. BACK OUT OF SLSS
    SET SLSSERR4 = TO SAVE CHAGES, BEFORE ATTEMPTING COMPRESS.
    SET SLSSERR5 =
    SET SLSSERRX = PRESS ENTER TO CONTINUE.
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    EXIT
  END DO
  SET SLSSWRN1 = THIS UTILITY REQUIRES THAT THE SLSS TABLE FILE
  SET SLSSWRN2 = IS NOT CURRENTLY BEING UPDATED.
  SET SLSSWRN3 = THIS DIALOG WILL TERMINATE SO THAT A COMPRESS
  SET SLSSWRN4 = OF THE SLSS TABLE FILE MAY BE PERFORMED.
  SET SLSSWRN5 =
  SET SLSSWRNX = PRESS ENTER TO CONTINUE, END KEY TO TERMINATE.
  ISPEXEC DISPLAY PANEL(SLSS@WRN)
  SET RC = &LASTCC
  IF &RC ^= 0 THEN EXIT
  ISPEXEC TBOPEN SLSSCNTL NOWRITE LIBRARY(SLSSTLIB)
  SET RC = &LASTCC
  IF &RC ^= 0 THEN DO
    SET SLSSERR1 = UNABLE TO OPEN SLSS CONTROL FILE, SLSSCNTL.
    SET SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBOPEN' WAS &RC..
    SET SLSSERR3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
    SET SLSSERR4 = REGARDING THIS ERROR.
    SET SLSSERR5 =
    SET SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC TBEND SLSSCNTL
    EXIT
  END DO
LOOP: +
  ISPEXEC TBVCLEAR SLSSCNTL
  ISPEXEC TBSKIP SLSSCNTL NUMBER(+1)
  SET RC = &LASTCC
  IF &RC = 8 THEN GOTO DONE
  IF &RC ^= 0 THEN DO
    SET SLSSWRN1 = UNABLE TO OBTAIN CONTROL FILE INFORMATION
    SET SLSSWRN2 = FOR ONE OF THE LIBRARIES. PROCESSING WILL
    SET SLSSWRN3 = CONTINUE FOR REMAINING LIBRARIES.
    SET SLSSWRN4 = RETURN CODE FOR 'ISPEXEC TBSKIP' WAS &RC.
    SET SLSSWRN5 =
    SET SLSSWRNX = PRESS ENTER TO CONTINUE.
    ISPEXEC DISPLAY PANEL(SLSS@WRN)
    GOTO LOOP
  END DO
  SET SLSS = &STR(&CIBN)
  ISPEXEC VPUT (SLSS DEBUG) SHARED
  %SLSSENQ ENQTEST
  SET RC = &LASTCC
  IF &RC = 0 THEN GOTO LOOP
  ELSE DO
    SET SLSSERR1 = LIBRARY '&SLSS' IS CURRENTLY BEING UPDATED.
    SET SLSSERR2 = COMPRESS MAY NOT CONTINUE AT THIS TIME.
    SET SLSSERR3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
    SET SLSSERR4 = REGARDING THIS ERROR.
    SET SLSSERR5 =
    SET SLSSERRX = PRESS ENTER TO CONTINUE.
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND SLSSCNTL
    EXIT
  END DO
DONE: +
  ISPEXEC TBEND SLSSCNTL
  SET SLSS = &STR(MAIN)
  ISPEXEC VPUT (SLSS DEBUG) SHARED
  %SLSSENQ ENQALL
  IF &LASTCC ^= 0 THEN EXIT
  SET CHGFLG = &STR(C)
  ISPEXEC VPUT (CHGFLG) SHARED
  ISPEXEC TBEND  SLSSPROF
  ISPEXEC TBOPEN SLSSPROF
  SET RC = &LASTCC
  IF &RC = 8 THEN DO
    %SLSSPROF &SYSUID &DEBUG
    SET PROFRC = &LASTCC
    IF &PROFRC ^= 0 THEN DO
      SET SLSSWRN1 = UNABLE TO CREATE YOUR SLSS PROFILE.
      SET SLSSWRN2 = BE SURE JOB CARDS ARE CORRECT BEFORE SUBMITTING JOBS.
      SET SLSSWRN3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
      SET SLSSWRN4 = REGARDING THIS ERROR.
      SET SLSSWRN5 =
      SET SLSSWRNX = PRESS ENTER TO CONTINUE
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      GOTO SUBMIT
    END DO
    SET RC = 0
  END DO
  IF &RC ^= 0 THEN DO
    SET SLSSWRN1 = UNABLE TO OPEN YOUR SLSS PROFILE.
    SET SLSSWRN2 = RETURN CODE FOR 'ISPEXEC TBOPEN' WAS &RC..
    SET SLSSWRN3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
    SET SLSSWRN4 = REGARDING THIS ERROR.
    SET SLSSWRN5 =
    SET SLSSWRNX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@WRN)
    GOTO SUBMIT
  END DO
  ISPEXEC TBTOP SLSSPROF
  ISPEXEC TBVCLEAR SLSSPROF
  SET SLSSUSR = &STR(&SYSUID)
  ISPEXEC TBSKIP SLSSPROF
  SET SKIPRC = &LASTCC
  IF &SKIPRC ^= 0 THEN DO
    SET SLSSWRN1 = UNABLE TO OBTAIN INFORMATION FROM YOUR SLSS PROFILE.
    SET SLSSWRN2 = RETURN CODE FOR 'ISPEXEC TBSKIP' WAS &SKIPRC..
    SET SLSSWRN3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
    SET SLSSWRN4 = REGARDING THIS ERROR.
    SET SLSSWRN5 =
    SET SLSSWRNX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@WRN)
  END DO
  GOTO SUBMIT
SUBMIT: +
  IF &STR(&SLSSJOB1) = &STR() THEN DO
    ISPEXEC VGET (ZLLGJOB1 ZLLGJOB2 ZLLGJOB3 ZLLGJOB4)
    SET SLSSJOB1 = &STR(&ZLLGJOB1)
    SET SLSSJOB2 = &STR(&ZLLGJOB2)
    SET SLSSJOB3 = &STR(&ZLLGJOB3)
    SET SLSSJOB4 = &STR(&ZLLGJOB4)
    ISPEXEC DISPLAY PANEL(SLSSJOBC)
    SET RC = &LASTCC
    IF &RC = 8 THEN DO
      ISPEXEC TBEND SLSSPROF
      EXIT
    END DO
    IF &RC ^= 0 THEN DO
      SET SLSSERR1 = UNABLE TO DISPLAY SLSS JOB CARD SETUP PANEL.
      SET SLSSERR2 = RETURN CODE FOR 'ISPEXEC DISPLAY' WAS &RC..
      SET SLSSERR3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
      SET SLSSERR4 = REGARDING THIS ERROR.
      SET SLSSERR5 =
      SET SLSSERRX = PRESS ENTER TO CONTINUE
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND SLSSPROF
      EXIT
    END DO
  GOTO SUBMIT
  END DO
  SET SLSSINDX = &SLSSINDX + 1
  IF &SLSSINDX = 27 THEN SET SLSSINDX = 1
  SET TEMPNAME = &STR(//&SLSSUSR.&SUBSTR(&SLSSINDX:&SLSSINDX,&STR(ABCDEFGHIJKLMNOPQRSTUVWXYZ)))
  SET TEMPLEN = &LENGTH(&STR(&SLSSJOB1))
  SET SLSSJOB1 = &STR(&TEMPNAME.&SUBSTR(10:&TEMPLEN,&SLSSJOB1))
  ISPEXEC TBPUT SLSSPROF
  ISPEXEC TBCLOSE SLSSPROF
  ISPEXEC VPUT (SLSSJOB1 SLSSJOB2 SLSSJOB3 SLSSJOB4)
  ISPEXEC FTOPEN TEMP
  ISPEXEC FTINCL SLSSCOMP
  ISPEXEC FTCLOSE
  ISPEXEC VGET (ZTEMPF)
  IF &DEBUG ^= &STR(  ) THEN DO
    ISPEXEC EDIT  DATASET('&ZTEMPF')
  END DO
  SUBMIT '&ZTEMPF'
  EXIT
STOP: +
  SET SLSS = &STR(MAIN)
  ISPEXEC VPUT (SLSS DEBUG) SHARED
  %SLSSENQ ENQOFF
  EXIT
