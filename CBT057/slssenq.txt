PROC 1 ENQFLAG
  ISPEXEC VGET (SLSS DEBUG) SHARED
  IF &DEBUG = &STR(DEBUG) THEN DO
      CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
  END DO
  ELSE DO
      CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
  END DO
  ISPEXEC CONTROL ERRORS RETURN
  IF &STR(&ENQFLAG) = &STR(ENQOFF) THEN GOTO ENQOFF
ENQON: +
  ISPEXEC TBOPEN &SLSS.ENQ LIBRARY(SLSSTLIB)
  SET RC = &LASTCC
  IF &RC = 8 THEN DO
    EXIT CODE(00)
  END DO
  IF &RC ^= 0 THEN DO
    SET SLSSERR1 = UNABLE TO CHECK ENQUEUE ON LIBRARY &SLSS..
    SET SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBOPEN' WAS &RC..
    SET SLSSERR3 = PLEASE CONTACT TECHNICAL SERVICES FOR FURTHER
    SET SLSSERR4 = INFORMATION REGARDING THIS ERROR.
    SET SLSSERR5 =
    SET SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND &SLSS.ENQ
    EXIT CODE(20)
  END DO
  ISPEXEC TBSKIP &SLSS.ENQ NUMBER(+1)
  IF &STR(&ENQU) = &STR(&SYSUID) THEN DO
    ISPEXEC TBEND &SLSS.ENQ
    EXIT CODE(00)
  END DO
  IF &STR(&ENQU) = &STR(ENQALL) THEN DO
    SET SLSSERR1 = ALL LIBRARIES ARE UNAVAILABLE DUE TO COMPRESS
    SET SLSSERR2 = OR SUB LIBRARY BUILD FUNCTION.
    SET SLSSERR3 = PLEASE TRY PROCESS AT A LATER TIME.
    SET SLSSERR4 =
    SET SLSSERR5 =
    SET SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND &SLSS.ENQ
    EXIT CODE(20)
  END DO
  IF &STR(&ENQU) ^= &STR(   ) AND &STR(&ENQFLAG) = &STR(ENQTALL) THEN DO
    ISPEXEC TBEND &SLSS.ENQ
    EXIT CODE(00)
  END DO
  IF &STR(&ENQU) ^= &STR(    ) THEN DO
    SET SLSSERR1 = SELECTED LIBRARY IS CURRENTLY BEING UPDATED BY
    SET SLSSERR2 = USER '&ENQU'.  PLEASE TRY PROCESS AT A LATER
    SET SLSSERR3 = TIME.
    SET SLSSERR4 =
    SET SLSSERR5 =
    SET SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND &SLSS.ENQ
    EXIT CODE(20)
  END DO
  ELSE DO
    IF &STR(&ENQFLAG) = &STR(ENQTEST) OR &STR(&ENQFLAG) = &STR(ENQTALL) THEN DO
      ISPEXEC TBEND &SLSS.ENQ
      EXIT CODE(00)
    END DO
    IF &STR(&ENQFLAG) = &STR(ENQON) THEN DO
      SET ENQU = &SYSUID
    END DO
    IF &STR(&ENQFLAG) = &STR(ENQALL) THEN DO
      SET ENQU = &STR(ENQALL)
    END DO
    ISPEXEC TBPUT &SLSS.ENQ
    ISPEXEC TBCLOSE &SLSS.ENQ LIBRARY(SLSSTLIB)
    EXIT CODE(00)
  END DO
ENQOFF: +
  SET MOD = &STR(Y)
  ISPEXEC VPUT (MOD) SHARED
  ISPEXEC TBOPEN &SLSS.ENQ LIBRARY(SLSSTLIB)
  SET RC = &LASTCC
  IF &RC ^= 0 THEN DO
    SET SLSSERR1 = UNABLE TO REMOVE ENQUEUE ON LIBRARY &SLSS..
    SET SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBOPEN' WAS &RC..
    SET SLSSERR3 = PLEASE CONTACT TECHNICAL SERVICES FOR FURTHER
    SET SLSSERR4 = INFORMATION REGARDING THIS ERROR.
    SET SLSSERR5 =
    SET SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND &SLSS.ENQ
    EXIT CODE(20)
  END DO
  ISPEXEC TBSKIP &SLSS.ENQ NUMBER(+1)
  IF &STR(&ENQFLAG) = &STR(ENQOFF) THEN DO
    SET ENQU = &STR(    )
  END DO
  ISPEXEC TBPUT &SLSS.ENQ
  ISPEXEC TBCLOSE &SLSS.ENQ LIBRARY(SLSSTLIB)
  EXIT CODE(00)
