PROC 0 DEBUG
/*                                                               */
/*  PHONEMP - PRINT EMPLOYEES PHONE LIST                         */
/*                                                               */
CONTROL NOLIST NOMSG NOFLUSH PROMPT ASIS
IF &DEBUG NE &STR() THEN +
  CONTROL LIST MSG NOFLUSH PROMPT CONLIST SYMLIST ASIS
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC DISPLAY PANEL(PHONEMP)
IF &LASTCC = 8 THEN +
  DO
    ISPEXEC  SETMSG MSG(PHN009T)
    GOTO DONE
  END
ERROR +
  DO
    ISPEXEC  SETMSG MSG(PHN009Y)
    EXIT
  END
ISPEXEC  VGET (PHONORD PHONCOPY) ASIS
IF (&PHONORD = N) THEN +
  DO
    SET &OPTN = 1
  END
IF (&PHONORD = E) THEN +
  DO
    SET &OPTN = 2
  END
/*                                                               */
/*  CREATE SEQUENTIAL C271 FILE FROM PHONE TABLE                 */
/*                                                               */
ISPEXEC CONTROL DISPLAY REFRESH
ALLOC F(SYSUDUMP) SYSOUT(X)
ALLOC F(SYSOUT) SYSOUT(X)
ALLOC F(O271C) DA(&SYSUID..T271C) NEW SPACE(65 10) BLKSIZE(6160) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(80) RECFM(F B)
ERROR OFF
ISPEXEC SELECT PGM(PHONDUMP) NEWAPPL(PHN)
SET &RC = &LASTCC
FREE F(SYSUDUMP SYSOUT O271C)
ERROR +
  DO
    ISPEXEC  SETMSG MSG(PHN009Y)
    EXIT
  END
/*                                                               */
/*  SORT C271 BASED ON SORT OPTION                               */
/*                                                               */
ALLOC F(SYSUDUMP) DUMMY
ALLOC F(SYSOUT) DUMMY
ALLOC F(SYSIN) NEW SPACE(1 1) BLOCK(80) BLKSIZE(80) REUSE UNIT(SYSDA) RELEASE DELETE LRECL(80) RECFM(F)
OPENFILE SYSIN OUTPUT
IF (&PHONORD = N) THEN +
  DO
    SET &SYSIN EQ &STR( SORT FIELDS=(20,25,A),FORMAT=CH)
  END
IF (&PHONORD = E) THEN +
  DO
    SET &SYSIN EQ &STR( SORT FIELDS=(8,4,A),FORMAT=CH)
  END
PUTFILE SYSIN
CLOSFILE SYSIN
ALLOC F(SORTIN) DA(&SYSUID..T271C) SHR REUSE DELETE
ALLOC F(SORTOUT) DA(&SYSUID..T271CS) NEW SPACE(65 10) BLOCK(80) BLKSIZE(6160) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(80) RECFM(F B)
$ SORT
FREE F(SYSUDUMP SYSOUT SYSIN SORTIN SORTOUT)
/*                                                               */
/*  SORT HEADER (ACTUALLY CONVERTS LRECL=135 TO LRECL=132)       */
/*                                                               */
ALLOC F(SYSUDUMP) DUMMY
ALLOC F(SYSOUT) DUMMY
ALLOC F(SYSIN) NEW SPACE(1 1) BLOCK(80) BLKSIZE(80) REUSE UNIT(SYSDA) RELEASE DELETE LRECL(80) RECFM(F)
OPENFILE SYSIN OUTPUT
SET &SYSIN EQ &STR( SORT FIELDS=COPY)
PUTFILE SYSIN
SET &SYSIN EQ &STR( OUTREC FIELDS=(1,132))
PUTFILE SYSIN
CLOSFILE SYSIN
ALLOC F(SORTIN) DA(VIDEO.LONGER.DATA(PHONE)) SHR REUSE
ALLOC F(SORTOUT) DA(&SYSUID..HEADER) NEW SPACE(65 10) BLOCK(132) BLKSIZE(6204) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(132) RECFM(F B)
$ SORT
FREE F(SYSUDUMP SYSOUT SYSIN SORTIN SORTOUT)
/*                                                               */
/*  CREATE PHONE DIRECTORY                                       */
/*                                                               */
ALLOC F(SYSUDUMP) DUMMY
ALLOC F(SYSOUT) DUMMY
ALLOC F(SYS001) DA(&SYSUID..T271CS) SHR REUSE DELETE
ALLOC F(SYS002) DA(&SYSUID..PRT) NEW SPACE(50 5) BLOCK(133) BLKSIZE(6118) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(133) RECFM(F B M)
ALLOC F(SYS003) DA(&SYSUID..HEADER) OLD REUSE DELETE
SET &PARM EQ &STR(   &OPTN)
STEPLIB DATASET('MIS.LINKLIB') SHR
$ MIS03600 '&PARM'
STEPLIB
FREE F(SYSUDUMP SYSOUT SYS001 SYS002 SYS003)
/*                                                               */
/*  IF DIRECTORY IS TO BE PRINTED IN EMPLOYEE NAME ORDER         */
/*  SORT BEEPERS (ACTUALLY CONVERTS LRECL=135 TO LRECL=133)      */
/*                                                               */
IF (&PHONORD = N) THEN +
  DO
    ALLOC F(SYSUDUMP) DUMMY
    ALLOC F(SYSOUT) DUMMY
    ALLOC F(SYSIN) NEW SPACE(1 1) BLOCK(80) BLKSIZE(80) REUSE UNIT(SYSDA) RELEASE DELETE LRECL(80) RECFM(F)
    OPENFILE SYSIN OUTPUT
    SET &SYSIN EQ &STR( SORT FIELDS=COPY)
    PUTFILE SYSIN
    SET &SYSIN EQ &STR( OUTREC FIELDS=(1X,1,132))
    PUTFILE SYSIN
    CLOSFILE SYSIN
    ALLOC F(SORTIN) DA(VIDEO.LONGER.DATA(PHONEZ)) SHR REUSE
    ALLOC F(SORTOUT) DA(&SYSUID..BEEPER) NEW SPACE(50 5) BLOCK(133) BLKSIZE(6118) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(133) RECFM(F B M)
    $ SORT
    FREE F(SYSUDUMP SYSOUT SYSIN SORTIN SORTOUT)
  END
/*                                                               */
/*  PRINT DIRECTORY ON XEROX 9700                                */
/*                                                               */
  ALLOC F(SYSPRINT) DUMMY REUSE
  ALLOC F(SYSUDUMP) DUMMY REUSE
  ALLOC F(SYSUT1) DA(&SYSUID..PRT) OLD REUSE DELETE
  ALLOC F(SYSIN) DUMMY REUSE
  IF (&PHONORD = N) THEN +
    DO
      ALLOC F(SYSUT1A) DA(&SYSUID..BEEPER) OLD REUSE DELETE
      CONCAT (SYSUT1,SYSUT1A)
    END
  XRXALLOC DDNAME(SYSUT2) FORM(L001) REUSE
  DO WHILE (&PHONCOPY NE 0)
    $ IEBGENER
    SET &PHONCOPY = (&PHONCOPY - 1)
  END
  FREE F(SYSPRINT SYSUDUMP SYSUT1 SYSUT2 SYSIN)
  IF (&PHONORD = N) THEN +
    DO
      FREE F(SYSUT1A)
    END
ISPEXEC  SETMSG MSG(PHN009W)
DONE: +
  SET &PHONCOPY = &STR( )
  ISPEXEC  VPUT (PHONCOPY) ASIS
  EXIT
END
