PROC 0
    ISPEXEC VGET (DEBUG SLSS CIBSRTKY CIBNUMBR LIBADMIN CIBOWNER)
    IF &DEBUG = DEBUG THEN DO
        CONTROL MSG LIST CONLIST SYMLIST
    END DO
    ELSE DO
        CONTROL NOMSG
    END DO
INIT: +
    ISPEXEC CONTROL ERRORS RETURN
    ISPEXEC TBOPEN &SLSS NOWRITE LIBRARY(SLSSTLIB)
    SET &RC = &LASTCC
    IF &RC = 12 THEN DO
      SET &SLSSWRN1 = LIBRARY IS CURRENTLY OPEN FOR EDIT BY ANOTHER
      SET &SLSSWRN2 = USER.  PLEASE TRY PROCESS AT A LATER TIME.
      SET &SLSSWRN3 =
      SET &SLSSWRN4 =
      SET &SLSSWRN5 =
      SET &SLSSWRNX = PRESS ENTER TO CONTINUE
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      ISPEXEC TBEND &SLSS
      EXIT
    END DO
    IF &RC ^= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO OPEN LIBRARY &SLSS.
      SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBOPEN' WAS &RC..
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      EXIT
    END DO
    ISPEXEC TBTOP &SLSS
PROCESS: +
    ISPEXEC CONTROL DISPLAY REFRESH
    ISPEXEC TBDISPL &SLSS PANEL(SLSSBROW)
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      ISPEXEC TBEND &SLSS
      ISPEXEC TBEND MAINLOCS
      EXIT
    END DO
    IF &STR(&OPT) ^= &STR( ) THEN DO
      ISPEXEC TBVCLEAR &SLSS
      IF &STR(&OPT) ^= &STR(F) THEN DO
        SET &SLSID = &STR(&OPT.*)
      END DO
      ELSE DO
        ISPEXEC TBSKIP &SLSS ROW(&ROW#) NOREAD
      END DO
      ISPEXEC TBSARG &SLSS
      ISPEXEC TBSCAN &SLSS ROWID(ROW#) NEXT
      SET &LCC = &LASTCC
      IF &LCC = 0 THEN GOTO PROCESS
      IF &LCC = 8 THEN DO
        ISPEXEC CONTROL ERRORS RETURN
        SET &SLSSCAU1 = &STR(ENTRY '&SLSID' NOT FOUND IN LIBRARY)
        SET &SLSSCAU2 =
        SET &SLSSCAU3 =
        SET &SLSSCAU4 =
        SET &SLSSCAU5 =
        SET &SLSSCAUX = PRESS ENTER TO CONTINUE
        ISPEXEC DISPLAY PANEL(SLSS@CAU)
        GOTO PROCESS
      END DO
    END DO
    IF &RC = 0 THEN DO
      GOTO DISPLAY
    END DO
    IF &RC = 12 THEN DO
      GOTO DISPLAY
    END DO
    ISPEXEC CONTROL ERRORS RETURN
    SET &SLSSERR1 = UNABLE TO BROWSE LIBRARY &SLSS.
    SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBDISPL' WAS &RC..
    SET &SLSSERR3 =
    SET &SLSSERR4 =
    SET &SLSSERR5 =
    SET &SLSSERRX = PRESS ENTER TO CONTINUE, ELSE END KEY TO TERMINATE.
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    SET &RC = &LASTCC
    IF &RC = 8 THEN EXIT
DISPLAY: +
    ISPEXEC DISPLAY PANEL(SLSSBRW2)
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      GOTO PROCESS
    END DO
    IF &LOCS = Y THEN GOTO PROCLOCS
    GOTO DISPLAY
PROCLOCS: +
    IF &LOCSOPEN = Y THEN GOTO SHOWLOCS
    ISPEXEC TBOPEN MAINLOCS NOWRITE LIBRARY(SLSSTLIB)
    SET &RC = &LASTCC
    IF &RC ^= 0 THEN DO
      SET &SLSSERR1 = ERROR OPENING LIBRARY MAINLOCS
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBOPEN' WAS &RC.
      SET &SLSSERR3 = CONTACT TECHNICAL SERVICE FOR FURTHER INFORMATION
      SET &SLSSERR4 = CONCERNING THIS ERROR.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND MAINLOCS
      GOTO DISPLAY
    END DO
    SET &LOCSOPEN = Y
SHOWLOCS: +
    SET &LOCID = &STR(MAIN&SLSID)
    ISPEXEC TBGET MAINLOCS
    ISPEXEC DISPLAY PANEL(SLSSLOC1)
    GOTO DISPLAY
